import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidMultiplatformLibrary)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
}

kotlin {
    jvm()

    androidLibrary {
        namespace = "com.github.ravenzip.berezaUI"
        compileSdk = libs.versions.android.compileSdk.get().toInt()
        minSdk = libs.versions.android.minSdk.get().toInt()

        compilerOptions { jvmTarget = JvmTarget.JVM_17 }

        androidResources { enable = true }

        withHostTest { isIncludeAndroidResources = true }
    }

    js(IR) { browser() }

    @OptIn(ExperimentalWasmDsl::class) wasmJs { browser() }

    @OptIn(ExperimentalKotlinGradlePluginApi::class)
    dependencies {
        implementation(libs.compose.runtime)
        implementation(libs.compose.foundation)
        implementation(libs.compose.material3)
        implementation(libs.compose.ui)
        implementation(libs.compose.components.resources)
        implementation(libs.compose.uiToolingPreview)
        implementation(libs.androidx.lifecycle.viewmodelCompose)
        implementation(libs.androidx.lifecycle.runtimeCompose)

        // Модули bereza ui
        implementation(projects.berezaCore)

        // Модули других библиотек собственного производства
        implementation(libs.ravenzip.kotlinReactiveForms.core)

        testImplementation(libs.kotlin.test)
    }
}

dependencies { androidRuntimeClasspath(libs.compose.uiTooling) }
